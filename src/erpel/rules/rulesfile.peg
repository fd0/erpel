package rules

type ruleParser Peg {
    ruleState
}

# this is the entry point to the grammar
start <- Fields (Separator Templates (Separator Samples)?)? EOF

Fields <- S (Field EOL?)* Field?
Field <- "field" s FieldName s "{" statements "}" S

FieldName <- < [a-zA-Z0-9-_]+ >                       { p.newField(buffer[begin:end]) }

statements <- (line EOL)* line?

line <- (Comment / Statement)? s

Name <- < [a-zA-Z0-9-_]+ >                            { p.name = buffer[begin:end] }
Statement <- s Name s '=' s String Comment?           { p.set(p.name, p.value) }


String <- DoubleQuotedString / SingleQuotedString / RawString

SingleQuotedString <- < "'" ( "\\'" / !EOL !"'" . )* "'" > { p.value = buffer[begin:end] }
DoubleQuotedString <- < '"' ( '\\"' / !EOL !'"' . )* '"' > { p.value = buffer[begin:end] }
RawString <- < "`" ( !"`" . )* "`" >                       { p.value = buffer[begin:end] }

Separator <- s "---" "-"* s EOL

Templates <- (!Separator (Comment / Template) EOL)*
Template <- s <(!EOL .)*>                             { p.addTemplate(buffer[begin:end]) }

Samples <- ((Comment / Sample) EOL)*
Sample <- s <(!EOL .)*>                               { p.addSample(buffer[begin:end]) }

# comment to the end of the line
Comment <- s '#' (!EOL .)*

EOF <- !.
EOL <- [\r\n]

# Space
s <- [ \t]*

# lines with comments
S <- s Comment? (Comment? EOL)*
