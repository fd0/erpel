package config

type erpelParser Peg {
    configState
}

# this is the entry point to the grammar
start <- (line EOL)* line? EOF

line <- S (Comment / Section / Statement)? S

Section <- "[" < ( !"]" !EOL . )+ > "]"                    { p.newSection(buffer[begin:end]) }

Statement <- Name S '=' S Value                            { p.set(p.name, p.value) }

Name <- < (!'=' !EOL .)+ > { p.name = buffer[begin:end] }

Value <- DoubleQuotedValue / SingleQuotedvalue / RawValue

SingleQuotedvalue <- < "'" ( "\'" / !EOL !"'" . )* "'" >   { p.value = buffer[begin:end] }
DoubleQuotedValue <- < '"' ( '\"' / !EOL !'"' . )* '"' >   { p.value = buffer[begin:end] }
RawValue <- < ( !EOL . )* >                                { p.value = buffer[begin:end] }

# comment to the end of the line
Comment <- S '#' (!EOL .)*

EOF <- !.
EOL <- [\r\n]
S <- [ \t]*
