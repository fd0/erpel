package config

type erpelParser Peg {
    configState
}

# this is the entry point to the grammar
start <- (line EOL)* line? EOF

line <- _ (Comment / Statement)? _

Statement <- Name _ '=' _ Value { p.set(p.name, p.value) }

Name <- < (!'=' .)+ > { p.name = buffer[begin:end] }

Value <- DoubleQuotedValue / SingleQuotedvalue / RawValue

SingleQuotedvalue <- < "'" ( "\'" / !EOL !"'" . )* "'" >   { p.value = buffer[begin:end] }
DoubleQuotedValue <- < '"' ( '\"' / !EOL !'"' . )* '"' >   { p.value = buffer[begin:end] }
RawValue <- < ( !EOL . )* >                                { p.value = buffer[begin:end] }

# comment to the end of the line
Comment <- _ '#' (!EOL .)*

Any <- !EOL
EOF <- !.
EOL <- [\r\n]
_ <- [ \t]*
