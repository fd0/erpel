package config

type erpelParser Peg {
    configState
}

# this is the entry point to the grammar
start <- s statements s Section* statements EOF

statements <- (line EOL)* line?

line <- s (Comment / Statement)? s

Section <- SectionName s "{" statements "}" s              { p.setDefaultSection() }
SectionName <- < Name >                                    { p.newSection(buffer[begin:end]) }

Statement <- Name s '=' s Value                            { p.set(p.name, p.value) }

Name <- < [a-zA-Z0-9-_]+ >                                 { p.name = buffer[begin:end] }

Value <- DoubleQuotedValue / SingleQuotedvalue / RawValue

SingleQuotedvalue <- < "'" ( "\'" / !EOL !"'" . )* "'" >   { p.value = buffer[begin:end] }
DoubleQuotedValue <- < '"' ( '\"' / !EOL !'"' . )* '"' >   { p.value = buffer[begin:end] }
RawValue <- < ( !EOL . )* >                                { p.value = buffer[begin:end] }

# comment to the end of the line
Comment <- s '#' (!EOL .)*

EOF <- !.
EOL <- [\r\n]

# Space
s <- [ \t]*
