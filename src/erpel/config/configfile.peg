package config

type erpelParser Peg {
    configState
}

# this is the entry point to the grammar
start <- (line EOL)* line? EOF

line <- _ (Comment / Section / Statement)? _

Section <- "[" < ( !"]" !EOL . )+ > "]"                    { p.newSection(buffer[begin:end]) }

Statement <- Name _ '=' _ Value                            { p.set(p.name, p.value) }

Name <- < (!'=' !EOL .)+ > { p.name = buffer[begin:end] }

Value <- DoubleQuotedValue / SingleQuotedvalue / RawValue

SingleQuotedvalue <- < "'" ( "\'" / !EOL !"'" . )* "'" >   { p.value = buffer[begin:end] }
DoubleQuotedValue <- < '"' ( '\"' / !EOL !'"' . )* '"' >   { p.value = buffer[begin:end] }
RawValue <- < ( !EOL . )* >                                { p.value = buffer[begin:end] }

# comment to the end of the line
Comment <- _ '#' (!EOL .)*

EOF <- !.
EOL <- [\r\n]
_ <- [ \t]*
